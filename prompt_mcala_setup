# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
# Changed by Andrew McAllister <andrew@mcallister.science> for colors that I prefer
# and for anaconda python environment.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Andrew McAllister <andrew@mcallister.science>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png

function prompt_mcala_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(${_prompt_mcala_colors[1]}%b%f%u%c${_prompt_mcala_colors[4]}●%f)"
  else
    branch_format="(${_prompt_mcala_colors[1]}%b%f%u%c)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_mcala_setup{
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_mcala_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mcala_colors=(
      "%F{61}"  # purple
      "%F{64}" # Orange
      "%F{33}"  # blue
      "%F{160}" # Hotpink
      "%F{136}" # Yellow
      "%F{254}" #white
      "%F{166}" #orange
    )
  else
    _prompt_mcala_colors=(
      "%F{cyan}"
      "%F{green}"
      "%F{blue}"
      "%F{red}"
      "%F{yellow}"
      "%F{white}"
      "%F{orange}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_mcala_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_mcala_colors[5]}%a%f)"
  local unstaged_format="${_prompt_mcala_colors[4]}●%f"
  local staged_format="${_prompt_mcala_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${_prompt_mcala_colors[3]}%n%f${_prompt_mcala_colors[6]} on ${_prompt_mcala_colors[2]}%m%f${_prompt_mcala_colors[6]} in ${_prompt_mcala_colors[5]}%~%f "'${vcs_info_msg_0_}'"
 ${_prompt_mcala_colors[6]}> "
  RPROMPT='${_prompt_mcala_colors[7]} ${CONDA_DEFAULT_ENV}'
}

prompt_mcala_setup "$@"
